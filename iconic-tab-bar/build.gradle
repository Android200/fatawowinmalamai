apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

version = project.ext.versionName

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode
        versionName project.ext.versionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile "com.andkulikov:transitionseverywhere:1.7.0"
}

def siteUrl = 'https://github.com/bigscreen/iconic-tab-bar'
def gitUrl = 'https://github.com/bigscreen/iconic-tab-bar.git'
def libName = 'com.bigscreen:iconic-tab-bar'
group = "com.bigscreen"


task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    classifier 'javadoc'
    from generateJavadocs.destinationDir
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libName
        desc = 'Android TabBar that follows Android BottomBar design pattern.'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = 'https://github.com/bigscreen/iconic-tab-bar/issues'
        licenses = ["Apache-2.0"]
        labels = ['android', 'bottom-bar', 'custom-view', 'tab', 'tab-layout', 'navigation']
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}